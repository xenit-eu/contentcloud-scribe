plugins {
    id 'java-platform'
}

apply from: "${rootDir}/gradle/publish.gradle"

publishing {
    publications {
        scribePlatform(MavenPublication) {
            from components.javaPlatform
        }
    }
}

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    api platform("io.spring.initializr:initializr-bom:${springInitializrVersion}")

    constraints {
        api "eu.xenit.contentcloud.bard:bard:${bardVersion}"

        api ('org.assertj:assertj-core') {
            version {
                strictly '3.19.0'
            }
            because '3.21 results in a NoSuchMethodException with initializr:0.11.0'
        }

        api project(':scribe-generator')
        api project(':scribe-drivers:scribe-driver-web')
        api project(':scribe-configurations:scribe-app')
        api project(':scribe-infrastructure')
    }
}

